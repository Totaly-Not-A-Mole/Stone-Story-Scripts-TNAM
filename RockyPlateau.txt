//  Variables
var attkCnt = 3
var timeSnap = 0
var hpPrcnt
//  HitPoint Percentage
hpPrcnt = hp * 100
hpPrcnt = hpPrcnt / maxhp
//  Necicary functions
//Heavy Hammer Ability
func Hammer()
  ?foe = foe.distance < 12
  ^ & item.CanActivate()
  ^ & item.GetCooldown("heavy") <= 0
    equip heavy hammer
    activate R
  ?item.right = "heavy"
  ^ & item.right.state = 2
    equip heavy hammer
    activate R
//mind stone dodge
func dodge(S, T)//state, Animation time
  ?foe.state = S & foe.time = T
        ?item.GetCooldown("mind") <= 0
          equipL mind stone
          equipR compound shield
//  main script
?loc = Rocky
//reset variables on loop
  ?loc.loop
    attkCnt = 3
    timeSnap = 0
//pickup items (ignore boulders for time)
  ?pickup.distance < 5
    equipL star stone
    equipR triskelion
  :
    equipL triskelion
    equipR compound shield
//scout fight (11 to 15 stars)
  ?foe = acronian_scout
    ?foe.debuffs.string ! "damage:1"
      equipL poison sword
      equipR compound shield
    :?hpPrcnt < 100
      equip the ghost slayer
    :
      equipL ice sword
      equipR compound shield
//the man himself
  ?foe = boss

    ?foe = phase1
      equipL fire sword
      equipR compound shield
      ?foe.debuffs.string ! "damage:1"
        equipL poison sword
        equipR compound shield
      :?foe.debuffs.string ! "Chill:6"
        equipL ice sword
        equipR compound shield
      :?hpPrcnt < 50
        equip the ghost slayer

    ?foe = phase2
      ?foe = poison
        equipL ice sword
        equipR compound shield
      :?foe = ice
        equipL fire sword
        equipR compound shield
      :?foe = fire
        equipL aether wand
        equipR compound shield
      :?foe = aether
        equip the ghost slayer
      :?foe = vigor
        equipL poison sword
        equipR compound shield

    ?foe = phase3
//time snapshot, prevents attack count tracker from
//increasing more than once per attack animation,
//used to detect when this bitch is gonna gain armor
//so we can hammer ability beforehand
      ?foe.state = 32 & timeSnap < time 
      ^|foe.state = 115 & timeSnap < time
        timeSnap = time
        timeSnap = timeSnap + 100
        ?attkCnt >= 6 
          attkCnt = 0
        attkCnt = attkCnt + 1
      equipL fire sword
      equipR compound shield
      ?foe.armor >= 50
        equip heavy hammer
      :?foe.debuffs.string ! "Chill:6"
        equipL ice sword
        equipR compound shield
      :?foe.debuffs.string ! "damage:1"
        equipL poison sword
        equipR compound shield
      :?hpPrcnt <= 20
        activate potion
      :?hpPrcnt < 100
        equip the ghost slayer
      :
        equipL fire sword
        equipR compound shield
      ?attkCnt >= 6 & foe.state ! 107
        Hammer()
        equip heavy hammer
        ?loc.stars <= 13
          dodge(32, 35)
        :
          dodge(32, 50)
      dodge(115, 60)